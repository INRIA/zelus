open Data
open Misc
open Aux
  
let dt = 0.01 (* s     *)
let l = 15.0 (* m     *)
let g = 10.0 (* m/s^2 *)
let b = 1.5  (* 1/s *)
(* mass of the pendulum *)
let m = 5.

let node integr(x') = Misc.integr(dt, x')
let node deriv(x) = Misc.deriv(dt, x)

(*
  equation of the pendulum  :
  - theta angle of the pendulum with Oy
  - d2x0,d2y0 second degree derivatives of the botom coordinates
  - l length of the pendulum
  - g acceleration  *)
let node pendulum(theta0, x'', y'') = theta where
  rec
      theta = integr(integr((sin thetap) *. (y'' +. g *. m)
                            -.(cos thetap) *. x'') /. l)
  and
      thetap = theta0 fby theta
  
(* Second version with loss in energy *)
let node pendulum_with_loss (theta0, x0'', y0'') = theta where
  rec
      theta = integr(integr((sin thetap) *. (y0'' +. g *. m)
                              -.(cos thetap)*. x0'') /. l
                       -. b *. thetap)
  and
      thetap = theta0 fby theta
  

let node pendulum_with_moving_bottom(theta0, x0, y0) =
(* pendulum controled by hand:        *)
(* x0, y0 are the botom coordinates   *)
  
(* derivation *)
  
  let x0'' = deriv (deriv x0) in
  let y0'' = deriv (deriv y0) in
  
(* pendulum *)
  
  let theta = pendulum_with_loss(theta0, x0'', y0'') in
  
(* geometry *)
  
  let x = x0 +. l*. (sin theta) in
  let y = y0 +. l*. (cos theta) in
  (x, y, theta)

(* the finger is close to the bottom part of the pendulum *)
let eps = 0.4

let discrete close (x0, y0, x, y) = (x -. x0 <= eps) && (y -. y0 <= eps)

(* The main entry for the pendulum *)
(* read of inputs *)
(*- 's' to start;
 *- 'f' to freeze;
 *- 'r' to resume; *)
let node mpendulum(theta0) = 
   let key = Aux.input () in (* get inputs from the keyboard *)
   let x0i, y0i = Data.get_cursor () in (* get current position of the mouse *)
   local theta, x, y, x0, y0
   do  init theta = theta0 (* initial angle is 0.0 *)
   and init x = x0i (* initial value for x is x0i *)
   and init y = y0i (* initial value for y is y0i *)
   and init x0 = x0i (* initial value for x0 is x0i *)
   and init y0 = y0i (* initial value for y0 is y0i *)
   and automaton
       | Await ->
           (* await for the start button to be pressed *)
           do until (key = Start) on (close(x0, y0, x0i, y0i)) then Running
       | Running ->
           (* run the pendulum *)
	   do (x, y, theta) = pendulum_with_moving_bottom(theta0, x0i, y0i)
           and x0 = x0i and y0 = y0i
	   until (key = Freeze) then Freeze
       | Freeze ->
           let c = close(x0, y0, x0i, y0i) in
	   (* do nothing. When the resume button is pressed *)
	   (* continue, that is, resume the running mode *)
	   do until (key = Resume) on c continue Running
	   else (key = Start) on c then Running
       end in
   (x0, y0, x, y, theta)
   
(* do something once; for the moment, the language forces *)
(* global declarations to be of the form [let name = ... ]. We could add *)
(* either [let pat = ...] or expressions at top level or both. *)

let n =
  print_string
    "Press 's' to start; \
           'f' to freeze; \
           'r' to resume; \
           'q' to quit\n";
  print_string
    "To be effective, the mouse pointer must be \n\
     close to the base of the pendulum";
  print_newline ()
		   
let node manual () =
  let (x0, y0, x, y, _) = mpendulum(0.0) in
  let p = make_pend(x0, y0, x, y) in
  draw_pendulum(p, p -> pre p)
