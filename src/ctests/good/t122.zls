(* A water tank. Introductory example of *)
(* Abstract Interpretation with Infinitesimals by *)
(* Kido, Chaudhuri, Hasuo, 2015 *)

(* Prouver que [1 <= x <= 12] ? *)
   
(* A timer *)
let hybrid timeout(k) = alarm where
  rec der t = 1.0 init -. k
  and alarm = up(t)

(* Await for zero-crossing on [z]. Emit [go] after [k] seconds. *)
let hybrid delay(z, k) = go where
  rec automaton
      | Await -> do unless up(z) then Run
      | Run -> do unless (timeout(k)) then do emit go = () in Await
      end

   
(* A two modes water tank. In the [On] mode, the level [x] is increased *)
(* by a factor of 1.0. It is decreased by a factor of 2.0 in the mode *)
(* [Off]. *)
let hybrid water() =
  let rec
      init x = 1.0
  and
      automaton
      | On ->
          (* [1 <= x <= 12] *)
          do der x = 1.0
	  unless (delay(last x -. 10.0, 2.0))() then Off
      | Off ->
	  (* [1 <= x <= 12] *)
          do der x = -2.0
	  unless (delay(5.0 -. last x, 2.0))() then On
      end in
  x
